apply plugin: 'com.android.application'

/**
 * Use the property (key/value pais format) file secrets.properties
 * to configure api keys for your project.
 * Here is the list of necessary keys:
 * - DEBUG_GOOGLE_BOOKS_API_KEY
 * - RELEASE_GOOGLE_BOOKS_API_KEY
 */
def Properties projectProperties = new Properties()
projectProperties.load(new FileInputStream(new File(projectDir, '/secrets.properties')))


android {
    compileSdkVersion 23
    buildToolsVersion "23.0.0"

    defaultConfig {
        applicationId "ecarrara.eng.vilibra"
        minSdkVersion 18 // Jellybean
        targetSdkVersion 23 // Marshmallow
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            buildConfigField "String", "GOOGLE_BOOKS_API_KEY", "\"" + projectProperties.DEBUG_GOOGLE_BOOKS_API_KEY + "\""
        }
        release {
            buildConfigField "String", "GOOGLE_BOOKS_API_KEY", "\"" + projectProperties.RELEASE_GOOGLE_BOOKS_API_KEY + "\""

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'LICENSE.txt'
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'com.android.support:design:23.0.1'
    compile 'me.dm7.barcodescanner:zbar:1.5' // for barcode scanning
    compile 'com.squareup.retrofit:retrofit:1.9.0'

    // unit test dependencies
    testCompile 'junit:junit:4.12'

    // dependencies for instrumentation tests
    androidTestCompile 'com.android.support.test:runner:0.3'
    androidTestCompile 'com.android.support.test:rules:0.3' // to use JUnit4 rules

    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2'
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.1'
    androidTestCompile 'org.hamcrest:hamcrest-integration:1.3'
}
