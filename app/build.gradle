apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

/**
 * Use the property (key/value pais format) file secrets.properties
 * to configure api keys for your project.
 * Here is the list of necessary keys:
 * - DEBUG_GOOGLE_BOOKS_API_KEY
 * - RELEASE_GOOGLE_BOOKS_API_KEY
 */
def Properties projectProperties = new Properties()
projectProperties.load(new FileInputStream(new File(projectDir, '/secrets.properties')))

ext {
    ANDROID_SUPPORT_LIBS_VERSION = '26.0.1'
    BUTTERKNIFE_VERSION = '8.5.1'
    DAGGER_VERSION = '2.10'
    RETROFIT_VERSION = '2.3.0'
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"

    defaultConfig {
        applicationId "br.eng.ecarrara.vilibra"
        minSdkVersion 21 // Lollipop
        targetSdkVersion 26 // Android O
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            buildConfigField "String", "GOOGLE_BOOKS_API_KEY", "\"" + projectProperties.DEBUG_GOOGLE_BOOKS_API_KEY + "\""
        }
        release {
            buildConfigField "String", "GOOGLE_BOOKS_API_KEY", "\"" + projectProperties.RELEASE_GOOGLE_BOOKS_API_KEY + "\""

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {

        String testData = 'src/testdata'

        main {
            java.srcDirs += 'src/main/kotlin'
        }

        test {
            java.srcDirs += 'src/test/kotlin'
            resources.srcDirs += [testData]
        }

        androidTest {
            java.srcDirs += 'src/androidTest/kotlin'
            resources.srcDirs += [testData]
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'LICENSE.txt'
    }

}

dependencies {

    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    androidTestCompile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Android Support Libraries
    compile "com.android.support:support-core-utils:${ANDROID_SUPPORT_LIBS_VERSION}"
    compile "com.android.support:support-fragment:${ANDROID_SUPPORT_LIBS_VERSION}"
    compile "com.android.support:design:${ANDROID_SUPPORT_LIBS_VERSION}"
    compile "com.android.support:recyclerview-v7:${ANDROID_SUPPORT_LIBS_VERSION}"

    // Barcode Reading
    compile "com.journeyapps:zxing-android-embedded:3.5.0"

    // Butterknife
    compile "com.jakewharton:butterknife:${BUTTERKNIFE_VERSION}"
    kapt "com.jakewharton:butterknife-compiler:${BUTTERKNIFE_VERSION}"

    // Logging
    compile 'com.jakewharton.timber:timber:4.5.1'

    // Retrofit
    compile "com.squareup.retrofit2:retrofit:${RETROFIT_VERSION}"
    compile "com.squareup.retrofit2:converter-gson:${RETROFIT_VERSION}"

    // Dagger
    compile "com.google.dagger:dagger:${DAGGER_VERSION}"
    kapt "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"

    // RxJava
    compile "io.reactivex.rxjava2:rxandroid:${RX_JAVA_VERSION}"
    compile "io.reactivex.rxjava2:rxjava:${RX_JAVA_VERSION}"
    compile "com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0"

    // Unit Tests Dependencies
    testCompile project(':vilibra-test-utils')

    testCompile "com.android.support.test:runner:${ANDROID_TEST_SUPPORT_LIBS_VERSION}"
    testCompile "com.android.support.test:rules:${ANDROID_TEST_SUPPORT_LIBS_VERSION}"

    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-integration:1.3'
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'org.mockito:mockito-core:2.8.47'

    // Instrumentationt Tests Dependencies
    androidTestCompile "com.android.support.test:runner:${ANDROID_TEST_SUPPORT_LIBS_VERSION}"
    androidTestCompile "com.android.support.test:rules:${ANDROID_TEST_SUPPORT_LIBS_VERSION}"

    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
    androidTestCompile 'org.hamcrest:hamcrest-integration:1.3'

}
repositories {
    mavenCentral()
}
